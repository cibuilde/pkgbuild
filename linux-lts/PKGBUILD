# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=linux-lts
pkgver=5.15.65
pkgrel=1
pkgdesc='LTS Linux'
url="https://www.kernel.org/"
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc libelf pahole cpio perl tar xz
)
options=('!strip')
_srcname=linux-$pkgver
source=(
  https://cdn.kernel.org/pub/linux/kernel/v${pkgver%%.*}.x/${_srcname}.tar.{xz,sign}
  config         # the main kernel config file
  file://${PWD}/arch/0001-ZEN-Add-sysctl-and-CONFIG-to-disallow-unprivileged-C.patch
  file://${PWD}/arch/0002-PCI-Add-more-NVIDIA-controllers-to-the-MSI-masking-q.patch
  file://${PWD}/arch/0003-iommu-intel-do-deep-dma-unmapping-to-avoid-kernel-fl.patch
  file://${PWD}/arch/0004-Bluetooth-btintel-Fix-bdaddress-comparison-with-garb.patch
  file://${PWD}/arch/0005-lg-laptop-Recognize-more-models.patch
  file://${PWD}/arch/0006-Fix-NFSv4-mount-regression.patch
  file://${PWD}/patches/0001-ANDROID-Revert-Enable-Werror-by-default-for-all-kern.patch
  file://${PWD}/patches/0002-FROMLIST-fs-Generic-function-to-convert-iocb-to-rw-f.patch
  file://${PWD}/patches/0003-UPSTREAM-fuse-fix-matching-of-FUSE_DEV_IOC_CLONE-com.patch
  file://${PWD}/patches/0004-FROMLIST-fuse-32-bit-user-space-ioctl-compat-for-fus.patch
  file://${PWD}/patches/0005-FROMLIST-fuse-Definitions-and-ioctl-for-passthrough.patch
  file://${PWD}/patches/0006-FROMLIST-fuse-Passthrough-initialization-and-release.patch
  file://${PWD}/patches/0007-FROMLIST-fuse-Introduce-synchronous-read-and-write-f.patch
  file://${PWD}/patches/0008-FROMLIST-fuse-Handle-asynchronous-read-and-write-in-.patch
  file://${PWD}/patches/0009-FROMLIST-fuse-Use-daemon-creds-in-passthrough-mode.patch
  file://${PWD}/patches/0010-FROMLIST-fuse-Introduce-passthrough-for-mmap.patch
  file://${PWD}/patches/0011-FROMLIST-fuse-Allocate-unlikely-used-ioctl-number-fo.patch
  file://${PWD}/patches/0012-ONHOLD-ANDROID-binder-add-support-for-RT-prio-inheri.patch
  file://${PWD}/patches/0013-CHROMIUM-cgroups-relax-permissions-on-moving-tasks-b.patch
  file://${PWD}/patches/0014-ANDROID-uid_cputime-Adds-accounting-for-the-cputimes.patch
  file://${PWD}/patches/0015-ANDROID-taskstats-track-fsync-syscalls.patch
  file://${PWD}/patches/0016-ANDROID-cpufreq-track-per-task-time-in-state.patch
  file://${PWD}/patches/0017-ANDROID-cpufreq-create-dummy-cpufreq-driver.patch
  file://${PWD}/patches/0018-NOUPSTREAM-ANDROID-mm-add-a-field-to-store-names-for.patch
  file://${PWD}/patches/0019-ANDROID-mm-Throttle-rss_stat-tracepoint.patch
  file://${PWD}/patches/0020-ANDROID-mm-cma-retry-allocations-in-cma_alloc.patch
  file://${PWD}/patches/0021-ONHOLD-FROMLIST-dma-buf-dma-heap-Keep-track-of-the-h.patch
  file://${PWD}/patches/0022-ONHOLD-FROMLIST-dma-buf-system_heap-Add-a-system-unc.patch
  file://${PWD}/patches/0023-ANDROID-dma-buf-Add-support-for-partial-cache-mainte.patch
  file://${PWD}/patches/0024-ANDROID-dma-buf-Add-support-for-mapping-buffers-with.patch
  file://${PWD}/patches/0025-ANDROID-dma-buf-Add-support-to-get-flags-associated-.patch
  file://${PWD}/patches/0026-ANDROID-dma-heap-Add-proper-kref-handling-on-dma-buf.patch
  file://${PWD}/patches/0027-ANDROID-dma-heap-Refactor-code-to-allow-for-future-i.patch
  file://${PWD}/patches/0028-ANDROID-dma-heap-Rework-allocation-calls-to-return-s.patch
  file://${PWD}/patches/0029-NOUPSTREAM-ANDROID-usb-gadget-configfs-Add-Uevent-to.patch
  file://${PWD}/patches/0030-NOUPSTREAM-ANDROID-usb-gadget-f_accessory-Add-Androi.patch
  file://${PWD}/patches/0031-NOUPSTREAM-ANDROID-usb-gadget-f_midi-create-F_midi-d.patch
  file://${PWD}/patches/0032-NOUPSTREAM-ANDROID-usb-gadget-f_audio_source-New-gad.patch
  file://${PWD}/patches/0033-ANDROID-Allow-DRM_IOCTL_MODE_-_DUMB-for-render-clien.patch
  file://${PWD}/patches/0034-NOUPSTREAM-ANDROID-include-drm-increase-DRM-max-prop.patch
  file://${PWD}/patches/0035-ANDROID-fs-epoll-use-freezable-blocking-call.patch
  file://${PWD}/patches/0036-NOUPSTREAM-ANDROID-fs-FS-tracepoints-to-track-IO.patch
  file://${PWD}/patches/0037-NOUPSTREAM-ANDROID-GKI-Don-t-compact-fuse_req-when-C.patch
  file://${PWD}/patches/0038-ANDROID-vfs-d_canonical_path-for-stacked-FS.patch
  file://${PWD}/patches/0039-ANDROID-fuse-Add-support-for-d_canonical_path.patch
  file://${PWD}/patches/0040-ANDROID-fs-fuse-Keep-FUSE-file-times-consistent-with.patch
  file://${PWD}/patches/0041-ANDROID-Initial-commit-of-Incremental-FS.patch
  file://${PWD}/patches/0042-ANDROID-Incremental-fs-Provide-self-tests.patch
  file://${PWD}/patches/0043-NOUPSTREAM-ANDROID-net-xfrm-make-PF_KEY-SHA256-use-R.patch
  file://${PWD}/patches/0044-ONHOLD-ANDROID-net-xfrm-always-export-pktgen_xfrm_ou.patch
  file://${PWD}/patches/0045-ANDROID-netfilter-xt_quota2-add-quota2-from-xtables-.patch
  file://${PWD}/patches/0046-ANDROID-xt_quota2-remove-trailing-junk-which-might-h.patch
  file://${PWD}/patches/0047-ANDROID-xt_quota2-clear-quota2_log-message-before-se.patch
  file://${PWD}/patches/0048-ANDROID-xt_quota2-set-usersize-in-xt_match-registrat.patch
  file://${PWD}/patches/0049-ANDROID-net-ipv6-autoconf-routes-into-per-device-tab.patch
  file://${PWD}/patches/0050-ANDROID-virt_wifi-Add-data-ops-for-scan-data-simulat.patch
  file://${PWD}/patches/0051-NOUPSTREAM-ANDROID-Unconditionally-create-bridge-tra.patch
  file://${PWD}/patches/0052-ANDROID-Temporarily-disable-XFRM_USER_COMPAT-filteri.patch
  file://${PWD}/patches/0053-ANDROID-netfilter-xt_IDLETIMER-Add-new-netlink-msg-t.patch
)
validpgpkeys=(
  'ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linus Torvalds
  '647F28654894E3BD457199BE38DBBDC86092693E'  # Greg Kroah-Hartman
)
# https://www.kernel.org/pub/linux/kernel/v5.x/sha256sums.asc
sha256sums=('6dd3cd1e5a629d0002bc6c6ec7e8ea96710104f38664122dd56c83dfd4eb7341'
            'SKIP'
            '67b7379830e81ae0f3f787a8855e91a5b50df5a6c31e6ad9807c2db84281393a'
            '7bd64ff894475b3415d792ba8466ba7e8f872af56dbf1aeed0d261fe4008b8b5'
            '39649dc1dfcb06b411ad124e123769e955a78961b4ea17538c0919a930925549'
            '56c12551e859cc67520909e64feecbf1b190cee8addef150c5b9d1bb1d40981e'
            '5c1ee81fdd5818442af6081de987f9c1a9ce3c8d183566b3dfc19a8433aa3dde'
            '067e8995fcd6f6ed25e0253e9374c0e179a000c154da3e59ce62634945ac5be9'
            '95dad02b01937681af0a207e22a6bf64c33e067bf7a14cb98262dd8f69194eb8')

export KBUILD_BUILD_HOST=archlinux
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
  cd $_srcname

  echo "Setting version..."
  scripts/setlocalversion --save-scmversion
  echo "-$pkgrel" > localversion.10-pkgrel
  echo "${pkgbase#linux}" > localversion.20-pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done

  echo "Setting config..."
  cp ../config .config
  make olddefconfig
  diff -u ../config .config || :

  make -s kernelrelease > version
  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $_srcname
  make all
}

_package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(coreutils kmod initramfs)
  optdepends=('wireless-regdb: to set the correct wireless channels of your country'
              'linux-firmware: firmware images needed for some devices')
  provides=(VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE KSMBD-MODULE)
  replaces=(wireguard-lts)

  cd $_srcname
  local kernver="$(<version)"
  local modulesdir="$pkgdir/usr/lib/modules/$kernver"

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build and source links
  rm "$modulesdir"/{source,build}
}

_package-headers() {
  pkgdesc="Headers and scripts for building modules for the $pkgdesc kernel"
  depends=(pahole)

  cd $_srcname
  local builddir="$pkgdir/usr/lib/modules/$(<version)/build"

  echo "Installing build files..."
  install -Dt "$builddir" -m644 .config Makefile Module.symvers System.map \
    localversion.* version vmlinux
  install -Dt "$builddir/kernel" -m644 kernel/Makefile
  install -Dt "$builddir/arch/x86" -m644 arch/x86/Makefile
  cp -t "$builddir" -a scripts

  # required when STACK_VALIDATION is enabled
  install -Dt "$builddir/tools/objtool" tools/objtool/objtool

  # required when DEBUG_INFO_BTF_MODULES is enabled
  #install -Dt "$builddir/tools/bpf/resolve_btfids" tools/bpf/resolve_btfids/resolve_btfids

  echo "Installing headers..."
  cp -t "$builddir" -a include
  cp -t "$builddir/arch/x86" -a arch/x86/include
  install -Dt "$builddir/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

  install -Dt "$builddir/drivers/md" -m644 drivers/md/*.h
  install -Dt "$builddir/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "$builddir/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "$builddir/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "$builddir/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "$builddir/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "$builddir/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  echo "Installing KConfig files..."
  find . -name 'Kconfig*' -exec install -Dm644 {} "$builddir/{}" \;

  echo "Removing unneeded architectures..."
  local arch
  for arch in "$builddir"/arch/*/; do
    [[ $arch = */x86/ ]] && continue
    echo "Removing $(basename "$arch")"
    rm -r "$arch"
  done

  echo "Removing documentation..."
  rm -r "$builddir/Documentation"

  echo "Removing broken symlinks..."
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete

  echo "Removing loose objects..."
  find "$builddir" -type f -name '*.o' -printf 'Removing %P\n' -delete

  echo "Stripping build tools..."
  local file
  while read -rd '' file; do
    case "$(file -bi "$file")" in
      application/x-sharedlib\;*)      # Libraries (.so)
        strip -v $STRIP_SHARED "$file" ;;
      application/x-archive\;*)        # Libraries (.a)
        strip -v $STRIP_STATIC "$file" ;;
      application/x-executable\;*)     # Binaries
        strip -v $STRIP_BINARIES "$file" ;;
      application/x-pie-executable\;*) # Relocatable binaries
        strip -v $STRIP_SHARED "$file" ;;
    esac
  done < <(find "$builddir" -type f -perm -u+x ! -name vmlinux -print0)

  echo "Stripping vmlinux..."
  strip -v $STRIP_STATIC "$builddir/vmlinux"

  echo "Adding symlink..."
  mkdir -p "$pkgdir/usr/src"
  ln -sr "$builddir" "$pkgdir/usr/src/$pkgbase"
}

pkgname=("$pkgbase" "$pkgbase-headers")
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
